plugins {
    id 'groovy'
    id 'java'
    id "checkstyle"
    id 'jacoco'
    id 'application'
    id 'com.palantir.docker' version "0.36.0"
    id "org.sonarqube" version "5.0.0.4638"
}

allprojects {

    jacoco {
        toolVersion = "0.8.10"
    }

    jacocoTestReport {
        reports {
            xml.required = true
            csv.required = true
        }
    }

    checkstyle {
        toolVersion = "10.3.4"
        configFile = "gradle/checkstyle.xml" as File
        checkstyleTest.enabled = false
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    repositories {
        mavenCentral()
        maven {
            url = 'https://repository.jboss.org/nexus/content/repositories/releases/'
        }
    }

}

application {
    mainClass = 'org.free.Bootstrap'
}

docker {
    name 'tele-gpt:latest'
    files tasks.installDist.outputs
}

test {
    useJUnitPlatform()
    def cpus = Runtime.getRuntime().availableProcessors()
    maxParallelForks = (int) (cpus / 4) > 0 ? (int) (cpus / 4) : 1
    testLogging {
        events "started", "passed", "skipped", "failed", "standardOut", "standardError"
    }
    failFast = false
    finalizedBy jacocoTestReport
}

dependencies {

    compileOnly "org.projectlombok:lombok:1.18.20"
    annotationProcessor "org.projectlombok:lombok:1.18.20"

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.7.1'
    implementation 'ws.schild:jave-core:3.3.1'
    implementation 'ws.schild:jave-nativebin-linux64:3.3.1'
    implementation 'com.github.pengrad:java-telegram-bot-api:6.6.0'
    implementation 'com.theokanning.openai-gpt3-java:service:0.12.0'
    implementation 'org.slf4j:slf4j-log4j12:2.0.7'
    implementation 'com.github.pemistahl:lingua:1.2.2'
    implementation 'com.squareup.retrofit2:converter-jackson:2.9.0'

    testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'
    testImplementation 'net.bytebuddy:byte-buddy:1.15.7'
    testImplementation 'org.objenesis:objenesis:3.3'

}

sonarqube {
    properties {
        //property "sonar.host.url", System.getenv("SONAR_URL")
        //property "sonar.token", System.getenv("SONAR_TOKEN")
        property "sonar.qualitygate.wait", false
        property 'sonar.core.codeCoveragePlugin', 'jacoco'
    }
}